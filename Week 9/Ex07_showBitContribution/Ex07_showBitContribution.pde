/*
 Student: Lucas Cardoza
 Class: CSC 545: Computer Speech, Music, and Images, Spring 2021
 Exercise No. 7: Shows Contribution to Image of Each Bit of Pixel
 Instructor: Lloyd Smith
 Due Date: Sunday, March 14, 2021 11:59 PM
*/


//Shows contribution to image of each bit of pixel
//img[0] is original; img[1] is image generated by
//least significant bit; img[2] is next bit, etc
PImage[] img = new PImage[9];
String[] fnames = {"4.2.03.jpg", "4.2.07.jpg", "blueShrub.jpg", "bridge.jpg",
                   "eiffel.jpg", "saturn-moonx-large.jpg",
                   "temple.jpg", "waikiki.jpg"};
//masks[0] is a dummy - it is never used
int[] masks = {255, 1, 2, 4, 8, 16, 32, 64, 128};


void setup() 
{
  size(500, 500);
  surface.setResizable(true);
  img[0] = loadImage(fnames[7]);
  surface.setSize(img[0].width, img[0].height);
  
  //Create images
  for (int i = 1; i < img.length; i++) 
  {
    img[i] = makeImage(img[0], masks[i], 8 - i); //source img, mask, num bits to shift
    
    // Print data to consoul to see exatly waht values are being passed to makeImage
    println("\ni = " + (8 - i));
    println("mask[i] value = " + masks[i]);
  }
}


void draw() {}


//Makes a new image from the source by masking and shifting bits
PImage makeImage(PImage source, int mask, int shiftVal) 
{
  PImage target = source.get();  //Copy source image
  target.loadPixels();
  
  // Use ‘<<’ to shift left and ‘&’ to AND an integer with a mask
  for (int i = 0; i < target.pixels.length; i++)
  {
    color c = target.pixels[i];
    int r = int(red(c)) & mask;
    int g = int(green(c)) & mask;
    int b = int(blue(c)) & mask;
    
    r <<= shiftVal;
    g <<= shiftVal;
    b <<= shiftVal;
    target.pixels[i] = color(r, g, b);
  }
  
  target.updatePixels();
  return target;
}


void keyPressed() 
{
  if (key == '0') image(img[0], 0, 0);
  if (key == '1') image(img[1], 0, 0);
  if (key == '2') image(img[2], 0, 0);
  if (key == '3') image(img[3], 0, 0);
  if (key == '4') image(img[4], 0, 0);
  if (key == '5') image(img[5], 0, 0);
  if (key == '6') image(img[6], 0, 0);
  if (key == '7') image(img[7], 0, 0);
  if (key == '8') image(img[8], 0, 0);
}
